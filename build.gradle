plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
	id "io.github.uoxx3.project-environment" version "1.0.1"
}

sourceCompatibility = 1.11

def baseVersion = '3.9.4'
def baseGroupId = 'io.github.adempiere'
def grpcVersion = '1.65.1'
def protobufVersion = '3.25.4'

repositories {
    mavenLocal()
    mavenCentral()
	maven {
		// Custom GitHub Repository (ADempiere by Default)
		url = findProperty("deployRepository") ?: System.properties['deploy.repository'] ?:
			projectEnv.get("GITHUB_DEPLOY_REPOSITORY", null) ?: System.getenv("GITHUB_DEPLOY_REPOSITORY") ?:
			"https://maven.pkg.github.com/adempiere/adempiere"
		credentials {
			// project property, system property, .env project file, system enviroment variable
			username = findProperty("deployUsername") ?: System.properties['deploy.user'] ?:
				projectEnv.get("GITHUB_DEPLOY_USER", null) ?: System.getenv("GITHUB_DEPLOY_USER")
			password = findProperty("deployToken") ?: System.properties['deploy.token'] ?:
				projectEnv.get("GITHUB_DEPLOY_TOKEN", null) ?: System.getenv("GITHUB_DEPLOY_TOKEN")
		}
	}
	maven {
		// ADempiere organization maven github registry
		url = "https://maven.pkg.github.com/adempiere/adempiere"
		credentials {
			// project property, system property, .env project file, system enviroment variable
			username = findProperty("deployUsername") ?: System.properties['deploy.user'] ?:
				projectEnv.get("GITHUB_DEPLOY_USER", null) ?: System.getenv("GITHUB_DEPLOY_USER")
			password = findProperty("deployToken") ?: System.properties['deploy.token'] ?:
				projectEnv.get("GITHUB_DEPLOY_TOKEN", null) ?: System.getenv("GITHUB_DEPLOY_TOKEN")
		}
	}
}

allprojects {
    tasks.withType(Javadoc) {
    	options.addStringOption('Xdoclint:none', '-quiet')
   	}
}

dependencies {
   api fileTree(
        dir: 'lib',
        include: [
            '*.jar'
        ]
    )
	api "io.grpc:grpc-netty-shaded:${grpcVersion}"
	api "com.google.protobuf:protobuf-java:${protobufVersion}"
	api "com.google.protobuf:protobuf-java-util:${protobufVersion}"
	api "io.jsonwebtoken:jjwt-api:0.12.6"
	api "io.jsonwebtoken:jjwt-impl:0.12.6"
	api "io.jsonwebtoken:jjwt-jackson:0.12.6"

    //	ADempiere Core
    api "${baseGroupId}:base:${baseVersion}"
   	api "${baseGroupId}:adempiere-jwt-token:1.0.2"
}

sourceSets {
    main {
         java {
            srcDirs = ['src/main/java']
         }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}


def entityType = 'D'
group = "io.github.adempiere"
// project property, system property, .env project file, system enviroment variable
version = findProperty("deployVersion") ?: System.properties['deploy.version'] ?:
    projectEnv.get("GITHUB_DEPLOY_VERSION", null) ?: System.getenv("ADEMPIERE_LIBRARY_VERSION") ?:
    "local-1.0.0"

jar {
	manifest {
		attributes(
			"Implementation-Title": "ADempiere gRPC Utils",
			"Implementation-Version": version,
			"EntityType": entityType
		)
    }
}

publishing {
    repositories {
		mavenLocal()

		maven {
			// Adempiere GitHub Organization by Default
			url = findProperty("deployPublishUrl") ?: System.properties['deploy.publish_url'] ?:
				projectEnv.get("GITHUB_DEPLOY_URL", null) ?: System.getenv("GITHUB_DEPLOY_URL") ?:
				"https://maven.pkg.github.com/adempiere/adempiere-customizations"
			credentials {
				// project property, system property, .env project file, system enviroment variable
				username = findProperty("deployUsername") ?: System.properties['deploy.user'] ?:
					projectEnv.get("GITHUB_DEPLOY_USER", null) ?: System.getenv("GITHUB_DEPLOY_USER")
				password = findProperty("deployToken") ?: System.properties['deploy.token'] ?:
					projectEnv.get("GITHUB_DEPLOY_TOKEN", null) ?: System.getenv("GITHUB_DEPLOY_TOKEN")
			}
		}
    }
    publications {
        mavenJava(MavenPublication) {
			groupId = group
			artifactId = 'adempiere-grpc-utils'
			version = version
           	from components.java
           	pom {
                name = 'ADempiere gRPC Utils'
                description = 'Utils classes for start a gRPC easy using ADempiere'
                url = 'http://adempiere.io/'
                licenses {
                    license {
                        name = 'GNU General Public License, version 2'
                        url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
					developer {
						id = 'edwinbetanc0urt'
						name = 'Edwin Betancourt'
						email = 'EdwinBetanc0urt@outlook.com'
					}
                }
                scm {
                    connection = 'scm:git:git://github.com/adempiere/adempiere-grpc-utils.git'
                    developerConnection = 'scm:git:ssh://github.com/adempiere/adempiere-grpc-utils.git'
                    url = 'http://github.com/adempiere/adempiere-grpc-utils'
                }
            }
		}
	}
}


task cleanBuildPublishLocal(type: GradleBuild) {
	tasks = ['clean', 'build', 'publishToMavenLocal']
}


signing {
	def isReleaseVersion = !version.toString().startsWith("local") && !version.toString().endsWith("-SNAPSHOT")

	sign configurations.archives

	setRequired {
		// signing is required if this is a release version and the artifacts are to be published
		// do not use hasTask() as this require realization of the tasks that maybe are not necessary
		(isReleaseVersion || version.toString().equals("build")) && gradle.taskGraph.allTasks.any {
			it.equals(PublishToMavenRepository)
		}
	}
	def signingKey = findProperty("deploySigningKey")
    def signingPassword = findProperty("deploySigningPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
